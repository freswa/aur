# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sebastian BÃ¸e <sebastianbooe at gmail dot com>

pkgname=chisel
pkgver=3.4.3
pkgrel=1
pkgdesc='Constructing Hardware in a Scala Embedded Language'
arch=('any')
url='https://chisel.eecs.berkeley.edu/'
license=('BSD')
depends=('java-environment' 'verilator')
makedepends=('sbt' 'graphviz')
source=("https://github.com/freechipsproject/chisel3/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('54f07b9114b281e18968ac3967abd359811601a8defc373561b98c76a1d3abe3d08357fc661bd6e692e177ba3ad6b18a44c69c1982c19f3a74c84a7490c02eea')

build(){
  cd ${pkgname}3-${pkgver}
  sbt publishLocal
}

package() {
  cd ${pkgname}3-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname} "${pkgdir}"/usr/share/scala/${pkgname}/
  install -Dm644 target/scala-2.12/${pkgname}3_2.12-${pkgver}.jar "${pkgdir}"/usr/share/scala/${pkgname}/${pkgname}.jar
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}
